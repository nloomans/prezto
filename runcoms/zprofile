#
# Executes commands at login pre-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

#
# Browser
#

if [[ "$OSTYPE" == darwin* ]]; then
  export BROWSER='open'
fi

#
# Editors
#

# Set the editor to vscode if using the integrated vscode terminal
# The VSCODE env flag should be added manually in the vscode settings
if [[ -n $VSCODE ]]; then
  # `code -w` opens the file and waits for it to close, allowing commands like
  # git commit to use it.
  export VISUAL='code -w'
else
  # Just use vim otherwise, since we don't want to open vscode if we're not
  # already inside it.
  export VISUAL='vim'
fi

export EDITOR="$VISUAL"
export PAGER='less'

#
# Language
#

if [[ -z "$LANG" ]]; then
  export LANG='en_US.UTF-8'
fi

#
# Paths
#

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# Set the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Set the list of directories that Zsh searches for programs.
path=(
  /usr/local/{bin,sbin}
  $path
)

# Add globally installed npm packages to the path
if [ -d ~/.yarn/bin ]; then
  # `yarn global bin` is really show, so we hardcode the bin location
  path=(
    $HOME/.yarn/bin
    $path
  )
fi

#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
export LESS='-F -g -i -M -R -S -w -X -z-4'

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi
